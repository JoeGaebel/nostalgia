#= require applications/edit/base_view

class PortalView extends BaseView
  viewClass: '.portal-view'

  constructor: (options = {}) ->
    @app = options.app
    $(document).ready(@onDocumentReady)

  onDocumentReady: =>
    @createUIHash()
    @bindHandlers()
    @updateSelect()
    @createTipsies()

    @hide() unless window.currentSphere?

  createUIHash: ->
    @ui =
      $addPortal: $('#start-portal')
      $resetPortal: $('#reset-portal')
      $savePortal: $('#save-portal')
      $toSphereSelect: $('#to_sphere_id')
      $portalCaption: $('#portal_caption')

      $tipsy: $('#portal-tipsy')

      $deletePortal: $('#delete-portal')
      $deleteExplanation: $('.delete-portal-explanation')

  bindHandlers: ->
    PSV.on 'click', @onPSVClick
    PSV.on 'select-marker', @onSelectMarker

    @ui.$addPortal.click(@onAddPortalClicked)
    @ui.$resetPortal.click(@app.resetPageState)
    @ui.$savePortal.click(@onSavePortalClicked)
    @ui.$deletePortal.click(@onDeleteClick)

    @ui.$portalCaption.keyup _.debounce(@onPortalCaptionChanged, 300)
    @ui.$toSphereSelect.change(@onToSphereSelectChanged)

  updateSelect: ->
    @ui.$toSphereSelect.children().remove()

    for sphere in memory.spheres
      continue if currentSphere.id == sphere.id
      @ui.$toSphereSelect.append($('<option>',
        value: sphere.id,
        text: sphere.caption
      ))

  createTipsies: ->
    @ui.$tipsy.tipsy
      gravity: 'w'
      opacity: 1
      html: true
      title: ->
        """
          <p>
            Portals connect one photosphere to another.
          </p>
          <p>
            To add a portal, click the <b>Add</b> button then click the corners of a door.
            Then, select the photosphere to transition to, and give it a caption
          </p>
        """

  getPortalParams: (portal) ->
    params =
      id: window.currentSphere.id
      portal:
        polygon_px: portal.polygon_px
        to_sphere_id: portal.to_sphere_id
        content: portal.tooltip?.content

    JSON.stringify(params)

  deletePortal: (portal) =>
    portalId = portal.id.split('-')[1]
    @portalToDelete = portal

    $.ajax
      url: "/portals/#{portalId}?&sphere_id=#{currentSphere.id}"
      type: 'DELETE'
      contentType: 'application/json'
      success: @onDeletePortalSuccess
      error: @app.onAjaxError

  cleanPortalCache: (portal) ->
    cachedPortal = _.findWhere(currentSphere.portals, { id: portal.id })
    indexOfCachedPortal = currentSphere.portals.indexOf(cachedPortal)
    if indexOfCachedPortal isnt -1
      currentSphere.portals.splice(indexOfCachedPortal, 1)

  resetViewState: =>
    @isCreatingPortal = false
    @isDeletingPortal = false

    @ui.$addPortal.prop('disabled', false)
    @ui.$savePortal.prop('disabled', true)
    @ui.$resetPortal.prop('disabled', true)
    @ui.$deletePortal.prop('disabled', false)

    @ui.$toSphereSelect.hide()
    @ui.$portalCaption.hide()
    @ui.$deleteExplanation.hide()

    try
      PSV.getMarker('new-portal')
    catch
      return

    PSV.removeMarker('new-portal')

  freeze: ->
    @ui.$addPortal.prop('disabled', true)
    @ui.$savePortal.prop('disabled', true)
    @ui.$resetPortal.prop('disabled', true)
    @ui.$deletePortal.prop('disabled', true)

    @ui.$toSphereSelect.hide()
    @ui.$portalCaption.hide()
    @ui.$deleteExplanation.hide()

    @isCreatingPortal = false
    @isDeletingPortal = false

# Event Handlers


  onSelectMarker: (marker) =>
    if marker.isPolygon()
      if @isDeletingPortal
        @deletePortal(marker)

      if !@isCreatingPortal and !@isDeletingPortal
        @app.doTransition(marker)

  onPSVClick: (e) =>
    if @isCreatingPortal
      newPortal = PSV.getMarker('new-portal')
      newPortal.polygon_px = [] unless newPortal.polygon_px[0].length
      newPortal.polygon_px.push [e.texture_x, e.texture_y]

      PSV.updateMarker(newPortal, true)

      if newPortal.polygon_px.length > 2
        @ui.$savePortal.prop('disabled', false)

  onAddPortalClicked: =>
    @app.freezeOtherViews(@)
    @app.setCursorToPencil()

    @ui.$addPortal.prop('disabled', true)
    @ui.$resetPortal.prop('disabled', false)

    @ui.$toSphereSelect.show()
    @ui.$portalCaption.show()

    @ui.$deletePortal.prop('disabled', true)

    @isCreatingPortal = true
    caption = @ui.$portalCaption.val()

    PSV.addMarker
      id: 'new-portal',
      polygon_px: [[]]
      to_sphere_id: @ui.$toSphereSelect.val()
      tooltip:  if caption then { content: caption, position: 'right bottom' } else undefined
      svgStyle:
        fill: "url(#points)"
        stroke: "rgba(255, 50, 50, 0.8)"
        'stroke-width': "2px"

  onToSphereSelectChanged: =>
    if @isCreatingPortal
      newPortal = PSV.getMarker('new-portal')
      newPortal.to_sphere_id = @ui.$toSphereSelect.val()

      PSV.updateMarker(newPortal)

  onSavePortalClicked: =>
    portal = PSV.getMarker('new-portal')
    @app.resetCursor()

    $.ajax
      url: '/portals'
      type: 'POST'
      dataType: 'json'
      contentType: 'application/json'
      success: @onSavePortalSuccess
      error: @app.onAjaxError
      data: @getPortalParams(portal)

  onPortalCaptionChanged: =>
    if @isCreatingPortal
      newPortal = PSV.getMarker('new-portal')
      caption = @ui.$portalCaption.val()

      if caption
        newPortal.tooltip =
          content: caption
          position: 'right bottom'
      else
        delete newPortal.tooltip

      PSV.updateMarker(newPortal)

  onDeleteClick: =>
    @app.freezeOtherViews(@)
    @ui.$deleteExplanation.show()
    @ui.$addPortal.prop('disabled', true)
    @ui.$resetPortal.prop('disabled', false)
    @ui.$deletePortal.prop('disabled', true)

    @isDeletingPortal = true


# Promises


  onDeletePortalSuccess: =>
    PSV.removeMarker(@portalToDelete)
    @cleanPortalCache(@portalToDelete)

    @portalToDelete = null
    @app.resetPageState()

  onSavePortalSuccess: (savedRecord) =>
    portal = PSV.getMarker('new-portal')
    PSV.removeMarker('new-portal', false)
    PSV.addMarker(savedRecord)
    currentSphere.portals.push(savedRecord)

    @app.resetPageState()


window.PortalView = PortalView
