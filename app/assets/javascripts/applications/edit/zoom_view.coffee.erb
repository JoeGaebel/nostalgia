#= require applications/edit/base_view

class ZoomView extends BaseView
  viewClass: '.zoom-view'

  constructor: (options = {}) ->
    @app = options.app
    $(document).ready(@onDocumentReady)

  onDocumentReady: =>
    @createUIHash()
    @bindHandlers()
    @resetSliderValue()

    @hide() unless window.currentSphere?

  createUIHash: ->
    @ui = {}

    @ui.$slider = $('#default_zoom')
    @ui.slider = @ui.$slider.slider()

  bindHandlers: ->
    @ui.slider.on 'change', @onSliderChange
    @ui.slider.on 'slideStop', @saveZoomLevel

  resetSliderValue: ->
    @ui.$slider.slider('setValue', currentSphere?.defaultZoom || 50)

  getLevel: ->
    parseInt(@ui.$slider.val())

  onSliderChange: =>
    PSV.zoom(@getLevel())

  saveZoomLevel: =>
    level = @getLevel()
    currentSphere.defaultZoom = level

    $.ajax
      url: "/spheres/#{currentSphere.id}/zoom"
      type: 'POST'
      dataType: 'json'
      contentType: 'application/json'
      error: @app.onAjaxError
      data: JSON.stringify({ default_zoom: level })


window.ZoomView = ZoomView
